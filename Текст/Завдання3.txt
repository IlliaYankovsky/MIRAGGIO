
&НаСервере
Процедура СформироватьНаСервере()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицяДляОбробки.Дата,
		|	ТаблицяДляОбробки.Валюта
		|ПОМЕСТИТЬ ТаблицяДляОбробки
		|ИЗ
		|	&ТаблицяДляОбробки КАК ТаблицяДляОбробки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицяДляОбробки.Дата,
		|	ТаблицяДляОбробки.Валюта
		|ПОМЕСТИТЬ ДатаВалюта
		|ИЗ
		|	ТаблицяДляОбробки КАК ТаблицяДляОбробки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатаВалюта.Дата,
		|	ДатаВалюта.Валюта,
		|	МАКСИМУМ(КурсыВалют.Период) КАК Период
		|ПОМЕСТИТЬ ДатаКурсу
		|ИЗ
		|	ДатаВалюта КАК ДатаВалюта
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|		ПО ДатаВалюта.Валюта = КурсыВалют.Валюта
		|			И ДатаВалюта.Дата >= КурсыВалют.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатаВалюта.Дата,
		|	ДатаВалюта.Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатаКурсу.Дата,
		|	ДатаКурсу.Валюта,
		|	ДатаКурсу.Период,
		|	КурсыВалют.Курс
		|ПОМЕСТИТЬ КурсыВалютСрез
		|ИЗ
		|	ДатаКурсу КАК ДатаКурсу
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
		|		ПО ДатаКурсу.Валюта = КурсыВалют.Валюта
		|			И ДатаКурсу.Период = КурсыВалют.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицяДляОбробки.Дата,
		|	ТаблицяДляОбробки.Валюта,
		|	КурсыВалютСрез.Период,
		|	КурсыВалютСрез.Курс
		|ИЗ
		|	ТаблицяДляОбробки КАК ТаблицяДляОбробки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КурсыВалютСрез КАК КурсыВалютСрез
		|		ПО ТаблицяДляОбробки.Валюта = КурсыВалютСрез.Валюта
		|			И ТаблицяДляОбробки.Дата = КурсыВалютСрез.Дата";

	//Передаємо таблицю як віртуальну
	//Знаходимо всі різні дати та валюти в таблиці
	//Шукаємо максимальні періоди (наприклад на 10.12.2022 по валюті нема курсу, тому шукаємо найближчий період де є курс)
	Запрос.УстановитьПараметр("ТаблицяДляОбробки", Объект.КурсВалюти.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Объект.КурсВалюти.Очистить();
	Объект.КурсВалюти.Загрузить(РезультатЗапроса);
		
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры
